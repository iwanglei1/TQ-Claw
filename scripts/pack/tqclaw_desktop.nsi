; TQ-Claw Desktop NSIS installer. Run makensis from repo root after
; building dist/win-unpacked (see scripts/pack/build_win.ps1).
; Usage: makensis /DTQCLAW_VERSION=1.2.3 /DOUTPUT_EXE=dist\TQ-Claw-Setup-1.2.3.exe scripts\pack\tqclaw_desktop.nsi

!include "MUI2.nsh"
!define MUI_ABORTWARNING
; Use custom icon from unpacked env (copied by build_win.ps1)
!define MUI_ICON "${UNPACKED}\icon.ico"
!define MUI_UNICON "${UNPACKED}\icon.ico"

!ifndef TQCLAW_VERSION
  !define TQCLAW_VERSION "0.0.0"
!endif
!ifndef OUTPUT_EXE
  !define OUTPUT_EXE "dist\TQ-Claw-Setup-${TQCLAW_VERSION}.exe"
!endif

Name "TQ-Claw Desktop"
OutFile "${OUTPUT_EXE}"
InstallDir "$LOCALAPPDATA\TQ-Claw"
InstallDirRegKey HKCU "Software\TQ-Claw" "InstallPath"
RequestExecutionLevel user

!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_LANGUAGE "SimpChinese"

; Pass /DUNPACKED=full_path from build_win.ps1 so path works when cwd != repo root
!ifndef UNPACKED
  !define UNPACKED "dist\win-unpacked"
!endif

Section "TQ-Claw Desktop" SEC01
  SetOutPath "$INSTDIR"
  File /r "${UNPACKED}\*.*"
  WriteRegStr HKCU "Software\TQ-Claw" "InstallPath" "$INSTDIR"
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  ; Main shortcut - uses VBS to hide console window
  CreateShortcut "$SMPROGRAMS\TQ-Claw Desktop.lnk" "$INSTDIR\TQ-Claw Desktop.vbs" "" "$INSTDIR\icon.ico" 0
  CreateShortcut "$DESKTOP\TQ-Claw Desktop.lnk" "$INSTDIR\TQ-Claw Desktop.vbs" "" "$INSTDIR\icon.ico" 0
  
  ; Debug shortcut - shows console window for troubleshooting
  CreateShortcut "$SMPROGRAMS\TQ-Claw Desktop (Debug).lnk" "$INSTDIR\TQ-Claw Desktop (Debug).bat" "" "$INSTDIR\icon.ico" 0
SectionEnd

Section "Uninstall"
  Delete "$SMPROGRAMS\TQ-Claw Desktop.lnk"
  Delete "$SMPROGRAMS\TQ-Claw Desktop (Debug).lnk"
  Delete "$DESKTOP\TQ-Claw Desktop.lnk"
  RMDir /r "$INSTDIR"
  DeleteRegKey HKCU "Software\TQ-Claw"
SectionEnd
